# Maintainer: CriShoux <crishoux@crishoux.com>
pkgname=perplexity-ai
pkgver=1.0.0
pkgrel=1
pkgdesc="Perplexity AI wrapper"
arch=('x86_64')
url='https://github.com/ShouxTech/perplexity-ai'
license=('MIT')
depends=('electron36')
makedepends=('npm')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/ShouxTech/perplexity-ai/archive/refs/tags/v$pkgver.tar.gz"
    "$pkgname"
    'icon.png'
    "$pkgname.desktop"
)
# Generate sha256sums with `makepkg -g`
sha256sums=(
    '1307375c2beefd3a7130c22a4bd2869b18193e12b7345e73e2a25aaa8d010bec'
    'ce93921c084a3b9baf569f2c35fb1d1dd7ff935614d8e5d4d79378a28a2109a7'
    '61ce4b424aad7c77f2d54bfb51128640bf54532759d6ff1c7efd5d88d02a94de'
    '24f9cbd73e712552ab1a082c98dfd55d0509ef9ac4ec4561746c75945883f188'
)

build() {
    cd "$srcdir/$pkgname-$pkgver"
    npm install --omit=dev
}

package() {
    # Install source files to /usr/share
    install -d "$pkgdir/usr/share/$pkgname"
    cp -r "$srcdir/$pkgname-$pkgver"/. "$pkgdir/usr/share/$pkgname"

    # Make available from /usr/bin (for `perplexity-ai` command)
    install -Dm755 "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"

    install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"
    install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
